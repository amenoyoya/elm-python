# 型

## 型推論の利点

- コンパイラが型を分析することで、間違った値が使われたときにエラーメッセージによる警告が行われる
    - => 実行時のランタイムエラーが起きなくなる

### 型推論の例
以下の`toFullName`関数は、`firstName`, `lastName`というフィールドを持つレコードを引数にとる

```elm
toFullName person =
  person.firstName ++ " " ++ person.lastName
```

REPLで上記関数を定義してみると分かるが、この関数は以下のように型推論される

```elm
<function> : { a | firstName : String, lastName : String } -> String
```

このように型推論されることによって、`toFullName`関数は、上記のレコード以外を受け付けなくなる

例えば以下のように、`firstName`を`fistName`にtypoしたレコードを渡したり、そもそもレコード以外の値を渡したりすると、コンパイル時にエラーを見つけてもらえる

```elm
toFullName {fistName = "Hermann", lastName = "Hesse"}
{-
-- TYPE MISMATCH ----------------------------------------------------------- elm

The 1st argument to `toFullName` is not what I expect:

5|   toFullName {fistName="Hermann", lastName="Hesse"}
                ^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^
This argument is a record of type:

    { fistName : String, lastName : String }

But `toFullName` needs the 1st argument to be:

    { a | firstName : String, lastName : String }

Hint: Seems like a record field typo. Maybe firstName should be fistName?

Hint: Can more type annotations be added? Type annotations always help me give
more specific messages, and I think they could help a lot in this case!
-}


toFullName "amenoyoya"
{-
-- TYPE MISMATCH ----------------------------------------------------------- elm

The 1st argument to `toFullName` is not what I expect:

5|   toFullName "amenoyoya"
                ^^^^^^^^^^^
This argument is a string of type:

    String

But `toFullName` needs the 1st argument to be:

    { a | firstName : String, lastName : String }
-}
```

同じようなコードをJavaScriptで書くと、実行時に undefined エラーが起こるまで、ミスに気付かない可能性がある

これが型推論の利点である

***

## 型の基本

### プリミティブな型
プリミティブな型とは、文字列（String）や浮動小数点（Float）、真偽値（Bool）などである

Elmでは `<値> : <型名>` という形式で値を管理している

```elm
"hello"
--> "hello" : String

not True
--> False : Bool

7 / 2
--> 3.5 : Float

round 3.1415
--> 3 : Int
```

---

### 関数の型
関数は特定の型の値を引数にとり、特定の型の値を返すフィルターである

したがって、Elmにおける関数は `'<function>' : <引数の型> -> <戻り値の型>` という形式で管理される

```elm
-- 文字列（String）を受け取り、その長さ（Int）を返す関数
String.length
--> <function> : String -> Int

String.length "hello"
--> 5 : Int

-- 文字列以外の値を渡せば当然コンパイルエラーになる
String.length [1,2,3]
{-
-- TYPE MISMATCH ----------------------------------------------------------- elm

The 1st argument to `length` is not what I expect:

5|   String.length [1,2,3]
                   ^^^^^^^
This argument is a list of type:

    List number

But `length` needs the 1st argument to be:

    String
-}
```

### 複数の引数を持つ関数
ここで複数の引数を持つ関数について考える

```elm
-- <繰り返し回数>と<文字列>を引数として、<文字列>を<繰り返し回数>回繰り返した文字列を返す関数
String.repeat
--> <function> : Int -> String -> String
```

上記のように、複数の引数は、`->`でつなげられて表現されている

これが意味することは、**elmの関数は引数を一つしか持たない**ということである

つまり、`String.repeat`関数は、Int型の値（繰り返し回数）を受け取り、**新しい関数を返す**関数であるということである

```elm
-- String.repeat: Int型の値を引数とし、以下のような関数を返す
-- 新しい関数: String型の値を引数とし、String型の値を返す
String.repeat : Int -> (String -> String)

-- 例: String.repeatに<繰り返し回数>のみ渡す（部分適用）
String.repeat 4
--> <function> String -> String
-- ※ ここで作成される関数は、引数に渡された文字列を4回繰り返した文字列を返す関数である
```

この**部分適用**というテクニックが使えることによって、Elm（もしくは他の関数型言語）は関数の汎用性を高めることに成功している

#### パイプライン
他の関数型言語同様、Elmにも部分適用を利用するためのパイプ演算子が存在する

例えば、入力された文字列を整数に変換する`sanitize`関数が定義されているとする

```elm
-- sanitize関数の型注釈: Stringを受けてIntを返すことを期待
-- ※型注釈, Maybeについては後述
sanitize : String -> Maybe Int
sanitize input =
  String.toInt (String.trim input)
```

上記関数の処理の流れとしては、`String.trim`で文字列前後の空白を削除し => `String.toInt`で文字列を整数に変換している

これをパイプラインを使うことで、処理の流れ順に見やすく書くことができる

```elm
sanitize : String -> Maybe Int
sanitize input =
  input
    |> String.trim
    |> String.toInt
```

このパイプ演算子`|>`が、専用の構文ではなく、単なる関数として定義できるのは、部分適用というテクニックがあるからである

分かりやすく書くと、パイプ演算子は以下のように部分適用して新しい関数を作っている

```elm
パイプ: Any型の値を引数にとる
=> 引数の値の型推論をする => <Type>型と推論
=> (<Type>型の値を引数とする関数)を引数とする関数を返す
```

---

### 型注釈（タイプアノテーション）
Elmではコンパイラに型推論させるだけではなく、定義の上の行に型注釈を書き、型を明示させることもできる

```elm
-- half関数: Float型の値をとり、Float型の値を返すことを明示
half : Float -> Float
half n = n / 2

-- Int型の数値を引数に渡そうとするとエラーになる
intNumber = 7 // 2
--> 3 : Int
half intNumber
--> Error! function expects: Float
```

型注釈は必須ではないが、以下のような利点があることから、なるべく書くようにした方が良い

1. コンパイラに型を明示することでエラーメッセージの質を向上させることができる
2. 後でコードを見直す時、簡易的なドキュメントとして利用できる

---

### 型変数（タイプバリアブル）
`elm/core`の関数を見ると、小文字の**型シグネチャ**(※)がいくつかあることに気づく

※ 型シグネチャとは関数の引数の型と返り値の型の組み合わせのこと

```elm
List.length
--> <function> : List a -> Int
```

上記のように、型の中にある小文字aのようなものを**型変数**と呼ぶ

このaが実際にどんな型になるかは、`List.length`がどのように使われるかによって変わる

言い換えれば、List型とはその中に入る値の型が決まって初めて決まる型であり `List a` で一つの型になると考えることができる

#### 制約付き型変数
通常、型変数にはどのような型でも当てはめることができるが、いくつか"制約付き"の型変数もある

- `number`:
    - 型変数として`Int`か`Float`のみ当てはめることが可能
- `appendable`:
    - 型変数として`String`か`List a`のみ当てはめることが可能
- `comparable`:
    - 型変数として以下のいずれかの値で構成されるリストかタプルのみ当てはめることが可能
        - `Int`, `Float`, `Char`, `String`のいずれかで構成されている
        - `comparable`な値で構成されている
- `compappend`:
    - 型変数として`String`か`List comparable`のみ当てはめることが可能

これらの制約付き型変数は、`+`や`<`のような演算子をより柔軟に使えるようにするために存在している

---

### 型の別名（タイプエイリアス）

#### 別名定義
Elmでは以下のような式で、型に別名を付けることができる

```elm
type alias <別名> = <型>
```

別名を付けることにより、短く明確な型注釈を書けるようになる

```elm
{- 以下の構造を持つレコードをUser型と定義
    - String型nameフィールド
    - String型bioフィールド
-}
type alias User = {
  name : String,
  bio : String
}

-- hasDecentBio関数: UserをBoolに変換する
-- user.bio の文字数が80を超えていればTrue
hasDecentBio : User -> Bool
hasDecentBio user =
  String.length user.bio > 80
```

#### レコードコンストラクタ
レコードの別名を定義するとレコードコンストラクタも一緒に生成されるため、以下のようにしてレコードを作ることができる

```elm
type alias User = {
  name : String,
  bio : String
}

-- 新規User作成
User "Tom" "Friendly Carpenter"
--> { bio = "Friendly Carpenter", name = "Tom" } : User
```

---

### カスタム型
複数の型のいずれかである型（複数の型を包括するような型）を**カスタム型**という

また、カスタム型に列挙されている型を**バリアント**と呼ぶ

例えば、「男性」バリアントと「女性」バリアントのいずれかである「性別」というカスタム型を定義すると以下のようになる

```elm
-- 性別 = 男性 OR 女性
type Sex = Male | Female

-- 人間レコード = { sex: Sex, name: String }
type alias Person = {
  sex : Sex,
  name : String
}

-- レコードを作成
thomas = Person Male "Thomas"
kate = Person Female "Kate"

{-
バリアントにはそれぞれ関連するデータを持たせることもできる
例えば、上記のようなPersonレコードの Male, Female バリアントにString型のデータを関連付けるとシンプルに書ける
-}
type Sex = Male String | Female String

-- 名前のデータはバリアントに直接付与されたため、レコード型は不要
thomas = Male "Thomas"
kate = Female "Kate"
```

#### 集合としての型
カスタム型とは、複数の型を包括したものであるため、型の集合であると見なせる

同様に、全ての型は値の集合であると考えることができる

- `Bool` は `True | False` の集合
- `Color` は `Red | Yello | Green` の集合
- `Int` は `... | -1 | 0 | 1 | ...` の集合
- `String` は `"" | "a" | "aa" | ... | "hello" | ...` の集合
